apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.microsoft.smartalarm"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
        arm {
            ndk {
                abiFilters "armeabi"
            }
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            jniLibs.srcDir 'libs'
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        dogfood {
            storeFile file("dogfoodkeystore.jks")
            storePassword 'dogfoodpassword'
            keyAlias 'dogfood'
            keyPassword 'dogfoodpassword'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.dogfood
        }
        debug {
            applicationIdSuffix '.dev'
        }
        all {
            manifestPlaceholders = [MIXPANEL_TOKEN: mixpanel_token, SPEECH_TOKEN: speech_token, VISION_TOKEN: vision_token, EMOTION_TOKEN: emotion_token, HOCKEYAPP_TOKEN: hockeyapp_token]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:preference-v7:23.1.1'
    compile 'com.android.support:preference-v14:23.1.1'
    compile 'com.microsoft.projectoxford:speechrecognition:0.5.0'
    compile 'com.microsoft.projectoxford:vision:0.5.0'
    compile 'com.microsoft.projectoxford:emotion:0.4.0'
    compile 'net.hockeyapp.android:HockeySDK:3.7.0-beta.1'

    compile "com.mixpanel.android:mixpanel-android:4.6.4"
    compile files('libs/gson-2.3.1.jar')
}

